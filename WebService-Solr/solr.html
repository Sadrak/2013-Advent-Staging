<?xml version="1.0" ?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<title></title>
<meta http-equiv="content-type" content="text/html; charset=utf-8" />
<link rev="made" href="mailto:johnk@johnklxopr.(none)" />
</head>

<body style="background-color: white">



<ul id="index">
  <li><a href="#TITLE">TITLE</a></li>
  <li><a href="#OVERVIEW">OVERVIEW</a></li>
  <li><a href="#INTRODUCTION">INTRODUCTION</a>
    <ul>
      <li><a href="#Solr-Basics">Solr Basics</a></li>
      <li><a href="#Perl-Modules-for-Solr">Perl Modules for Solr</a></li>
    </ul>
  </li>
  <li><a href="#Preparing-the-Environment">Preparing the Environment</a></li>
  <li><a href="#Create-A-Project-with-a-Template-Toolkit-View">Create A Project with a Template Toolkit View</a></li>
  <li><a href="#A-Thin-Model-with-Catalyst::Model::WebService::Solr">A Thin Model with Catalyst::Model::WebService::Solr</a>
    <ul>
      <li><a href="#About-the-thin-controller">About the thin controller</a>
        <ul>
          <li><a href="#response2info">response2info</a></li>
          <li><a href="#dump">dump</a></li>
          <li><a href="#example">example</a></li>
          <li><a href="#a-real-search">a real search</a></li>
        </ul>
      </li>
    </ul>
  </li>
  <li><a href="#Moving-to-a-Fat-Model">Moving to a Fat Model</a>
    <ul>
      <li><a href="#Adding-Updating-and-Deleting">Adding, Updating, and Deleting</a></li>
      <li><a href="#Add">Add</a></li>
      <li><a href="#Delete">Delete</a></li>
    </ul>
  </li>
  <li><a href="#For-More-Information">For More Information</a></li>
  <li><a href="#Summary">Summary</a></li>
  <li><a href="#Author">Author</a></li>
</ul>

<h1 id="TITLE">TITLE</h1>

<p>Using SOLR in a Catalyst Model with WebService::Solr</p>

<h1 id="OVERVIEW">OVERVIEW</h1>

<p>Using <a href="http://lucene.apache.org/solr/">Solr</a>, a Search Server from <a href="http://lucene.apache.org">Apache&#39;s Lucene Project</a> as a <b>Catalyst Model</b>.</p>

<h1 id="INTRODUCTION">INTRODUCTION</h1>

<p>Compared to conventional database search (and the full text query extensions found in most modern SQL implementations), a Search Server is going to provide better performance and search features. Since Solr is writeable as well as readable it can be used as a NoSQL datastore.</p>

<h2 id="Solr-Basics">Solr Basics</h2>

<p>Solr is a java servlet, implementing a web based interface to Lucene. Requests to Solr are made via HTTP requests. Request data may be sent in either POST or GET values. Data is returned in JSON but Solr will also return data in xml or CSV formats. Similarly POSTs of data to Solr may be in any of these formats. Lucene provides indexing and search technology, as well as spellchecking, hit highlighting and advanced analysis/tokenization capabilities. The Data Import Handler will allow you to import from lots of other sources rather than needing to POST it all through web requests. Once up and going it gives you a lot of possibilities for finding documents.</p>

<p>Solr runs in a java servelet answering HTTP protocol requests on a designated port, 8983 by default. Operations are carried out by Get requests and by Post requests of either XML or JSON. Data returned is also in JSON or XML format. CSV and a few other formats are also supported for data. Large data sets are usually imported through the Data Import Handler (DIH) which can among other methods load a CSV file or query a SQL database. The Server and the Collections (equivalent of a database) are configured through XML files. Solr does not include a crawling capability. The <a href="http://nutch.apache.org/">Nutch</a> utility or custom scripts are used in conjunction with Solr for crawling.</p>

<p>Requests to Solr can be made through a web browser or any utility such as wget, curl or LWP scripts that implements the HTTP protocol. Each request is independent of all others, there is no session or handshake, Solr recieves a request via HTTP and responds to it. The Solr Server also provides a web interface, anyone who can access the interface, can access all of its features. This has obvious architectural implications in securing solr, because security must be implemented at either the network level or by placing Solr behind another webserver which implements security. Normally Solr is not directly exposed to the public internet.</p>

<h2 id="Perl-Modules-for-Solr">Perl Modules for Solr</h2>

<p>There are a number of Perl Modules available for Solr, the two that appear the most viable are Apache::Solr and WebService::Solr. Unfortunately, all of the modules have problems both in bugs and unimplemented features. Initally I had the best luck with Apache::Solr but ran into some limitations there. After reading through the source code for several of the modules, I decided to work with WebService::Solr. At present it looks like Apache::Solr is being more actively developed so in the future it could become the better choice.</p>

<p>A lot of Perl&#39;s Solr users prefer to implement their own Agent/Model. Since the Solr interface is based on HTTP requests, JSON and XML, this is not hard so much as potentially time-consuming.</p>

<h1 id="Preparing-the-Environment">Preparing the Environment</h1>

<p>You will need to have a Catalyst Development Environment ready, in addition you should install WebService::Solr and Catalyst::Model::WebService::Solr. Day One of this Advent Calendar discussed the enhancements in 5.90050, for our purposes the improved UTF8 handling is a Critical Feature and resolves a showstopper bug. When I first looked at WebService::Solr &quot;Wide Character on Print&quot; really stymied my first sessions. If updating Catalyst isn&#39;t an option for you the workaround I was using is detailed in rtcpan bug 89288.</p>

<p>You will also need to install a JVM like open JDK and then download a copy of Solr from the <a href="http://lucene.apache.org/solr/downloads.html">Solr Download Page</a>. Once downloaded and extracted you will need to load the example data. Open up two terminals. To save space I&#39;ll refer you to the <a href="http://lucene.apache.org/solr/tutorial.html">Solr tutorial</a>, to speed up use post.sh in the exampledocs folder to populate the test data, and skip ahead to querying to confirm that you have loaded the 32 documents. For the purpose of the rest of the article it will be assumed you have Solr running locally with the test data loaded and answering the default port 8983.</p>

<pre><code> Terminal 1
 cd ..path_to../example
 java -jar start.jar
 
 Terminal 2
 cd ..path_to../example/exampledocs
 ./post.sh *.xml</code></pre>

<h1 id="Create-A-Project-with-a-Template-Toolkit-View">Create A Project with a Template Toolkit View</h1>

<pre><code> catalyst.pl SolrDemo
 cd SolrDemo
 ./script/solrdemo_create.pl view HTML TT</code></pre>

<p>Edit SolrDemo.conf</p>

<pre><code> solrserver         http://localhost:8983/solr/collection1</code></pre>

<p>Edit the config section of lib/SolrDemo.pm and add <b>encoding =&gt; &#39;utf8&#39;,</b> to prevent wide character errors.</p>

<pre><code> __PACKAGE__-&gt;config(
    name =&gt; &#39;SolrDemo&#39;,
    # Disable deprecated behavior needed by old applications
    disable_component_resolution_regex_fallback =&gt; 1,
    enable_catalyst_header =&gt; 1, # Send X-Catalyst header    
    encoding =&gt; &#39;utf8&#39;, # prevents wide character explosions
    &#39;View::HTML&#39; =&gt; {  #Set the location for TT files        
         INCLUDE_PATH =&gt; [ SolrDemo-&gt;path_to( &#39;root&#39; ), ], },    
 );</code></pre>

<p>Create some additional files we&#39;ll need</p>

<pre><code> touch lib/SolrDemo/Model/Solr.pm
 touch lib/SolrDemo/Model/SolrModelSolr.pm
 touch lib/SolrDemo/Controller/Thin.pm
 touch lib/SolrDemo/Controller/Thin.pm 
 touch root/results.tt
 touch t/model_solr.t</code></pre>

<p>In addition I recommend creating a reset script in the example/exampledocs folder of the Solr distribution. This script will delete your collection and replace it with the sample docs when you are testing CRUD operations.</p>

<pre><code> wget &quot;http://localhost:8983/solr/update?stream.body=&lt;delete&gt;&lt;query&gt;*:*&lt;/query&gt;&lt;/delete&gt;&quot; -O /dev/null
 wget &quot;http://localhost:8983/solr/update?stream.body=&lt;commit/&gt;&quot; -O /dev/null 
 ./post.sh *.xml</code></pre>

<h1 id="A-Thin-Model-with-Catalyst::Model::WebService::Solr">A Thin Model with Catalyst::Model::WebService::Solr</h1>

<p>There are only two options to configure, where to connect and autocommit. By default Solr may not immediately reflect changes to data, and the autocommit flag tells WebService::Solr to always follow delete/add operations with a commit request. You might want to turn it off if you were writing batches of data and wanted to commit at the end of the batch for performance purposes.</p>

<p>File: lib/SolrDemo/Model/SolrModelSolr</p>

<pre><code>    package SolrDemo::Model::SolrModelSolr;
    use namespace::autoclean;
    use Catalyst::Model::WebService::Solr;
    use Moose;

    extends &#39;Catalyst::Model::WebService::Solr&#39;;
    __PACKAGE__-&gt;config(
        server  =&gt; SolrDemo-&gt;config-&gt;{solrserver},
        options =&gt; { autocommit =&gt; 1, }
    );
    1;</code></pre>

<p>File: lib/SolrDemo/Controller/Thin.pm</p>

<pre><code>    package SolrDemo::Controller::Thin;
    use namespace::autoclean;
    use WebService::Solr::Query;
    use Moose;

    BEGIN { extends &#39;Catalyst::Controller&#39; }

    sub response2info {
        my $response = shift;
        my $raw      = $response-&gt;raw_response();
        my $pre      = &#39;&#39;;
        $pre .= &quot;\n_msg\n&quot; . $raw-&gt;{&#39;_msg&#39;};
        $pre .= &quot;\n_headers&quot;;
        my %hheaders = %{ $raw-&gt;{&#39;_headers&#39;} };
        for ( keys %hheaders ) { $pre .= &quot;\n    $_ = $hheaders{$_}&quot;; }
        $pre .= &quot;\n_request&quot;;
        my %rreq = %{ $raw-&gt;{&#39;_request&#39;} };
        for ( keys %rreq ) { $pre .= &quot;\n    $_ = $rreq{$_}&quot;; }
        $pre .= &quot;\n_content&lt;/pre&gt;\n&quot; . $raw-&gt;{&#39;_content&#39;} . &#39;&lt;pre&gt;&#39;;
        $pre .= &quot;\n_rc\n&quot; . $raw-&gt;{&#39;_rc&#39;};
        $pre .= &quot;\n_protocol\n&quot; . $raw-&gt;{&#39;_protocol&#39;};
        $pre .= &quot;\nRequest Status (via method)\n&quot; . $response-&gt;solr_status();
        my @docs = $response-&gt;docs;
        $pre .= &quot;\nDocument Count: &quot; . scalar(@docs);
        return $pre;
    }

    sub dump : Local : Args(0) {
        my ( $self, $c ) = @_;
        my $response =
        $c-&gt;model(&#39;SolrModelSolr&#39;)
        -&gt;search( WebService::Solr::Query-&gt;new( { &#39;*&#39; =&gt; \&#39;*&#39; } ),
            { rows =&gt; 10000 } );
        my @docs = $response-&gt;docs;
        $c-&gt;log-&gt;info( &quot;\nDocument Count: &quot; . scalar(@docs) );
        my $pre = &amp;response2info($response);
        $c-&gt;response-&gt;body(&quot;&lt;pre&gt;$pre &lt;/pre&gt;&quot;);
    }

    sub example : Local : Args(0) {
        my ( $self, $c ) = @_;
        my $response =
        $c-&gt;model(&#39;SolrModelSolr&#39;)
        -&gt;search( WebService::Solr::Query-&gt;new( { text =&gt; [&#39;hard drive&#39;] } ),
            { rows =&gt; 10000 } );
        my $pre = &amp;response2info($response);
        $c-&gt;response-&gt;body(&quot;&lt;pre&gt;$pre &lt;/pre&gt;&quot;);
    }

    __PACKAGE__-&gt;meta-&gt;make_immutable;

    1;</code></pre>

<h2 id="About-the-thin-controller">About the thin controller</h2>

<h3 id="response2info">response2info</h3>

<p>This is Viewish code shared by two of the methods that puts the raw elements of the response into a string.</p>

<h3 id="dump">dump</h3>

<p>Executes a query for all records in the Solr database. WebService::Solr::Query generates queries. To generate the query you need to pass a hashref of the Solr fields you want and the values for the fields, the \ indicates to pass the second * as a literal string. The second argument is a hashref of options, here we want to override the Solr default of returning 10 rows by specifying an arbitrary high value.</p>

<h3 id="example">example</h3>

<p>This example query is hard coded to find items matching the phrase &#39;hard drive&#39;, which, we see from the spew, gets translated as &#39;hard+drive&#39;. Here we specified the field text (which is a catch-all field defined to hold everything searchable) and passed an array ref to the list of values. If you copy and rename the method and then change the field list to <b>[&#39;hard drive&#39;,&#39;maxtor&#39;]</b>, you will find that you still get the same 2 records, this is because of solr&#39;s matching behaviour. If you want to filter for only maxtor hard drives you&#39;ll need to use a filter query (fq) which is specified in the options.</p>

<p>Add the following method to Thin.pm</p>

<pre><code>    sub maxtor :Local :Args(0) {
        my ( $self, $c ) = @_;
        my $maxq = WebService::Solr::Query-&gt;new( { manu =&gt; [&#39;maxtor&#39;] } );
        my $response =
        $c-&gt;model(&#39;SolrModelSolr&#39;)
        -&gt;search( WebService::Solr::Query-&gt;new( { text =&gt; [&#39;hard drive&#39;] } ),
            { rows =&gt; 10000, fq =&gt; $maxq } );
        my $pre = &amp;response2info($response);
        $c-&gt;response-&gt;body(&quot;&lt;pre&gt;$pre &lt;/pre&gt;&quot;);
    } </code></pre>

<h3 id="a-real-search">a real search</h3>

<p>Add this method to: Thin.pm</p>

<pre><code>    sub select : Local : Args(2) {
        my ( $self, $c, $fieldname, $fieldvalue ) = @_;
        my $response =
        $c-&gt;model(&#39;SolrModelSolr&#39;)
        -&gt;search( WebService::Solr::Query-&gt;new( { $fieldname =&gt; [$fieldvalue] } ),
            { rows =&gt; 10000 } );
        my @docs = $response-&gt;docs;
        $c-&gt;stash(
            template  =&gt; &#39;results.tt&#39;,
            field     =&gt; $fieldname,
            value     =&gt; $fieldvalue,
            docs      =&gt; \@docs,
        );
    }</code></pre>

<p>File: /root/results.tt</p>

<pre><code>    &lt;h1&gt;Catalyst SolrDemo&lt;/h1&gt;
    &lt;h2&gt;Docs in this query [% docs.list.size %]&lt;/h2&gt;
    &lt;h3&gt;Field [% field %] value [%value %]&lt;/h3&gt;
    &lt;table&gt;
    [% FOREACH doc IN docs %]
    &lt;tr&gt;&lt;th&gt;descriptor&lt;/th&gt;&lt;th&gt;field value&lt;/th&gt;&lt;/tr&gt;
    [% FOREACH fieldname IN doc.field_names.sort %]
    &lt;tr&gt;&lt;td&gt;[% fieldname %]&lt;/td&gt;&lt;td&gt;[% doc.value_for( fieldname ) %]&lt;/td&gt;&lt;/tr&gt;
    [% END %]
    [% END %]
    &lt;/table&gt;</code></pre>

<p>Try some queries:</p>

<ul>

<li><p>http://localhost:3000/thin/select/text/ipod</p>

</li>
<li><p>http://localhost:3000/thin/select/features/cache</p>

</li>
<li><p>http://localhost:3000/thin/select/manu/maxtor</p>

</li>
</ul>

<p>First the model returns a WebService::Solr::Response object, we use the docs method to extract an array of WebService::Solr::Document objects from it which are then passed by reference to the view. The view iterates the array and uses the <b>fieldnames</b> method to get a list of the fields in that document (not all documents in the test data have the same fields) and then iterates through it, retrieving the individual fields with the <b>value_for method</b>.</p>

<h1 id="Moving-to-a-Fat-Model">Moving to a Fat Model</h1>

<p>My Solr search queries typically require a lot of supporting code, which is easier to test in a model than a controller, and is generally more appropriate to the model. Unlike DBI-based models which maintain a connection, each request to Solr is completely independent of all others and no connection is maintained between them, so instantiating a new WebService::Solr object is relatively trivial, additionally if you work with multiple collections you need to create a seperate object for each one.</p>

<p>lib/SolrDemo/Model/Solr.pm</p>

<pre><code>    package SolrDemo::Model::Solr;

    use WebService::Solr;
    use WebService::Solr::Query;
    use WebService::Solr::Field ;
    use WebService::Solr::Document ;
    use namespace::autoclean;

    use parent &#39;Catalyst::Model&#39;;

    our $SOLR = WebService::Solr-&gt;new( SolrDemo-&gt;config-&gt;{solrserver} );

    sub _GeoFilter {
        my ( $location, $sfield, $distance ) = @_;
        return qq/\{!geofilt pt=$location sfield=$sfield d=$distance\}/;
    }

    sub List {
        my $self      = shift;
        my $params    = shift;
        my $mainquery = WebService::Solr::Query-&gt;new($params);
        my %options   = ( rows =&gt; 100 );
        my $response  = $SOLR-&gt;search( $mainquery, \%options );
        return $response-&gt;docs;
    }

    sub Kimmel {
        my $self         = shift;
        my $distance     = shift;
        my $kimmelcenter = &#39;39.95,-75.16&#39;;
        my $mainquery    = WebService::Solr::Query-&gt;new( { &#39;*&#39; =&gt; \&#39;*&#39; } );
        my $geofilt      = &amp;_GeoFilter( $kimmelcenter, &#39;store&#39;, $distance );
        my %options      = ( rows =&gt; 100, fq =&gt; $geofilt, sort =&gt; &#39;price asc&#39; );
        my $response     = $SOLR-&gt;search( $mainquery, \%options );
        return $response-&gt;docs;
    }

    1;</code></pre>

<p>t/model_solr.t</p>

<pre><code>    use Test::More;
    BEGIN { use_ok &#39;SolrDemo&#39; }

    my $C = SolrDemo-&gt;new ;
    my @docs = $C-&gt;model(&#39;Solr&#39;)-&gt;List( { cat =&gt; &#39;electronics&#39;, manu =&gt; &#39;corsair&#39; } );
    is( scalar(@docs), 2, &#39;We expect 2 docs&#39; );

    my $carnegiehall = &#39;40.76,-73.98&#39; ;
    my $geofilt = &amp;SolrDemo::Model::Solr::_GeoFilter( $carnegiehall, &#39;store&#39;, 400 ) ;
    is( $geofilt, &#39;{!geofilt pt=40.76,-73.98 sfield=store d=400}&#39;, 
        &#39;Test geofilter construction using carnegie hall as a testcase&#39;);
    my @docs2 = $C-&gt;model(&#39;Solr&#39;)-&gt;Kimmel( 1600 ) ;
    is( scalar(@docs2), 3, &#39;There are 3 items within 1600 km of the Kimmel Center&#39; );

    done_testing();</code></pre>

<p>lib/SolrDemo/Controller/Fat.pm</p>

<pre><code>    package SolrDemo::Controller::Fat;
    use namespace::autoclean;
    use Moose;

    BEGIN { extends &#39;Catalyst::Controller&#39; }

    sub select : Local : Args(2) {
        my ( $self, $c, $fieldname, $fieldvalue ) = @_;
        my @docs = $c-&gt;model(&#39;Solr&#39;)-&gt;List( { $fieldname =&gt; $fieldvalue } );
        $c-&gt;stash(
            template =&gt; &#39;results.tt&#39;,
            field    =&gt; $fieldname,
            value    =&gt; $fieldvalue,
            docs     =&gt; \@docs,
        );
    }

    sub nearkimmel : Local : Args() {
        my ( $self, $c ) = @_;
        my $distance = 500;
        my @docs     = $c-&gt;model(&#39;Solr&#39;)-&gt;Kimmel(500);
        $c-&gt;stash(
            template =&gt; &#39;results.tt&#39;,
            field    =&gt; &#39;Distance from Kimmel Center in Philadelphia&#39;,
            value    =&gt; $distance,
            docs     =&gt; \@docs,
        );
    }

    __PACKAGE__-&gt;meta-&gt;make_immutable;

    1;</code></pre>

<p>The Model contains 3 methods. The private method generates a geofilter string, because that isn&#39;t currently implemented in WebService::Solr, but I&#39;ve proposed it for a future release. Of the other two methods the first replicates the select we used in the thin model and the third finds things near the Kimmel Center in Philadelphia as an example of geospatial search.</p>

<p>A couple of times now we&#39;ve seen <b>WebService::Solr::Query-&gt;new( { &#39;*&#39; =&gt; \&#39;*&#39; } )</b>. If you go back to the first dump methods you can see it ended up as <b>(*%3A*)</b>, <b>%3A</b> translates back to &#39;<b>:</b>&#39;. You could use the string <b>&#39;(*:*)&#39;</b> instead of generating the value with Query. Modify the Kimmel method to demonstrate this yourself. In this special case we wanted to preseve the value &#39;*&#39; as a literal, not risk having it converted to <b>%2A</b>, which we accomplished by passing it as a reference. For this case it might be clearer to just use the string directly in your query, but generally I&#39;d rather use WebService::Solr::Query and have it worry about the details of Solr Grammar. WebService::Solr::Query is capable of generating complex queries with numerous options beyond the scope of this introduction.</p>

<p>I also added in the %options of the Kimmel method a third option, sort. The sort option takes two arguments, a field_name and either &#39;asc&#39; or &#39;desc&#39;.</p>

<p>You should now be able to run the tests, and if they work when you run the test server, /fat/select/?/? will work as it did in the thin model, and /fat/nearkimmel will show you results of a geospatial filter.</p>

<h2 id="Adding-Updating-and-Deleting">Adding, Updating, and Deleting</h2>

<p>We&#39;re now going to add a record, modify it, and then delete it. This is all going to be done in tests.</p>

<p>Two methods get added to the model, Delete and Add (which is also the update method). Both methods normally return a value of <b>1</b>, which is the value normally returned by the underlying WebService::Solr method, which in turn is determined by Solr&#39;s response, which is not necessarily an indicator that what you intended to happen is what happened.</p>

<h2 id="Add">Add</h2>

<p>Add and Update are the same method. When a document is added with the same id as an existing document, Solr replaces the original record with the new one. This means whenever we update a record we need to send the entire new version.</p>

<p>The Add method takes a hashref of fieldnames and values which it uses to create a WebService::Solr::Document. There is an optional parameter to the WebService::Solr-&gt;add method for setting boost values on fields, this is not implemented in our Model. A WebService::Solr::Document can be created in 3 ways, first it can be returned by a query to Solr, second it can be constructed from arrays of WebService::Solr::Field objects, and finally we can pass an array of array references to the constructructor.</p>

<p>Here is an example of a data structure to create a WebService::Solr::Document.</p>

<pre><code> my @fields = (
    [ id     =&gt; &#39;B0019032-02&#39;                     { boost =&gt; 1.6 } ],
    [ artist =&gt; &#39;Philadelphia Orchestra&#39;,       { boost =&gt; &#39;7.1&#39; } ],
    [ format =&gt; &#39;CD Audio&#39;                                         ],
    [ title  =&gt; &#39;Yannick Conducts Stravinsky: The Rite of Spring&#39;  ],
 );</code></pre>

<h2 id="Delete">Delete</h2>

<p>Delete takes a hashref that is used to construct a query. If we use { id =&gt; $VALUE } we will delete one record. A careless query could delete a lot of records, as the last test shows { cat =&gt; &#39;electronics&#39; } will delete half of our records! After you run the last test you will need to reset your data using the script you created earlier for that purpose. The sprintf statement is in the method because when the output of WebService::Solr::Query is fed to the delete method the delete method may recieve it is an object rather than a string.</p>

<p>Add to Solr.pm Model</p>

<pre><code>    sub Add {
        my $self      = shift;
        my $params    = shift;
        my @fields_array = () ;
        foreach my $k ( keys %{$params} ) { 
                my @fields = ( $k, $params-&gt;{ $k } );
                push @fields_array, ( \@fields ) ;
            }
        my $doc = WebService::Solr::Document-&gt;new( @fields_array ) || die &quot;cant newdoc $!&quot;;
        my $result = $SOLR-&gt;add( $doc ) ;
        return $result ;
    }

    sub Delete {
        my $self      = shift;
        my $params    = shift;
        # If the query isn&#39;t forcibly stringified an exception may be thrown.
        my $result = $SOLR-&gt;delete_by_query( 
            sprintf( &quot;%s&quot;, WebService::Solr::Query-&gt;new($params) ) ) ;
        return $result ;
    }</code></pre>

<p>Add to t/model_solr.t immediately above done_testing</p>

<pre><code>    note( &quot;\n* CRUD Tests *\n&quot; );
    my $added1 = $C-&gt;model(&#39;Solr&#39;)-&gt;Add(
        {
            name     =&gt; &#39;Zune Player&#39;,
            manu     =&gt; &#39;Microsoft&#39;,
            features =&gt; &#39;Truly Obsolete&#39;,
            price    =&gt; &#39;300&#39;,
            store    =&gt; &#39;40.76,-73.98&#39;,
            cat      =&gt; &#39;electronics&#39;,
            id       =&gt; &#39;MSZUNE&#39;
        }
    );
    is( $added1, 1,
        &#39;successfully added a zune located at Carnegie Hall to inventory&#39; );

    my @docs3 = $C-&gt;model(&#39;Solr&#39;)-&gt;Kimmel(1600);

    is( scalar(@docs3), 4,
        &#39;There are now 4 items within 1600 km of the Kimmel Center&#39; );

    # a subroutine to list a doc.
    sub listdoc {
        my $d      = shift;
        my $string = &#39;&#39;;
        $string .=
            &#39; ID: &#39;
        . $d-&gt;value_for(&#39;id&#39;) . &#39; -- &#39;
        . $d-&gt;value_for(&#39;name&#39;)
        . &#39; Manu: &#39;
        . $d-&gt;value_for(&#39;manu&#39;) . &quot;\n\t&quot;
        . $d-&gt;value_for(&#39;features&#39;);
        return $string;
    }

    note( &#39;* Display the 4 items within 1600km showing added record *&#39;);
    for (@docs3) { note( &amp;listdoc($_) ) }

    my $updated1 = $C-&gt;model(&#39;Solr&#39;)-&gt;Add(
        {
            name     =&gt; &#39;Zune Player&#39;,
            manu     =&gt; &#39;Microsoft&#39;,
            features =&gt; &#39;Half price Closeout Sale on our last MS ZUNE! Save $150&#39;,
            price    =&gt; &#39;150&#39;,
            store    =&gt; &#39;40.76,-73.98&#39;,
            cat      =&gt; &#39;electronics&#39;,
            id       =&gt; &#39;MSZUNE&#39;
        }
    );

    is( $updated1, 1, &#39;Updated the Zune for Closeout!&#39; );

    my @zunedocs = $C-&gt;model(&#39;Solr&#39;)-&gt;List( { id =&gt; &#39;MSZUNE&#39; } );
    my $zunedoc = $zunedocs[0];
    is( $zunedoc-&gt;value_for(&#39;price&#39;), 150, &#39;Prove that zune now costs $150&#39; );

    note( &#39;* Display the Documents showing modified record for ZUNE. *&#39;);
    @docs3 = $C-&gt;model(&#39;Solr&#39;)-&gt;Kimmel(1600);
    for (@docs3) { note( &amp;listdoc($_) ) }

    my $delete1 = $C-&gt;model(&#39;Solr&#39;)-&gt;Delete( { id =&gt; &#39;MSZUNE&#39; } );
    is( $delete1, 1, &#39;delete returned success&#39; );
    @zunedocs = $C-&gt;model(&#39;Solr&#39;)-&gt;List( { id =&gt; &#39;MSZUNE&#39; } );
    is( scalar( @zunedocs ) , 0, &#39;Confirm it is deleted&#39; );

    # This test deletes data, after running it you must reset your data
    # Comment it or skip it to avoid.
    my @before = $C-&gt;model(&#39;Solr&#39;)-&gt;List( { &#39;*&#39; =&gt; \&#39;*&#39; } );
    my $delete2 = $C-&gt;model(&#39;Solr&#39;)-&gt;Delete( { cat =&gt; &#39;electronics&#39; } );
    my @after = $C-&gt;model(&#39;Solr&#39;)-&gt;List( { &#39;*&#39; =&gt; \&#39;*&#39; } );
    is( scalar(@after) , 18, &quot;There were 32 documents, there are now 18&quot;);</code></pre>

<h1 id="For-More-Information">For More Information</h1>

<p>After following this how-to document you&#39;ll want to read the WebService::Solr Documentation. It is organized by sub-module so you&#39;ll have to read all of the pieces, plus the tests from the distribution which are where you&#39;ll find code examples. You&#39;ll also want to read the Solr Documentation, there is a lot more on the web about it than the module.</p>

<p>If there are any errata to this article they will be posted on my <a href="http://techinfo.brainbuz.org/?p=368">technical blog</a>. You can download the entire contents and source for this article as well <a href="http://www.brainbuz.org/images/solrcattut.tgz">http://www.brainbuz.org/images/solrcattut.tgz</a>.</p>

<h1 id="Summary">Summary</h1>

<p>In this article we created both Thin and Fat Models for WebService::Solr. For the Fat Model we also Created, Updated, and Destroyed data, and wrote tests for everything we did.</p>

<h1 id="Author">Author</h1>

<p>John Karr &lt;brainbuz@brainbuz.org&gt; brainbuz</p>

<p>Thanks to Andy Lester for taking time to review this article and make a few helpful recommendations.</p>


</body>

</html>


